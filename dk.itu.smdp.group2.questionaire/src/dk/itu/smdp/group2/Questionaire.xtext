grammar dk.itu.smdp.group2.Questionaire with org.eclipse.xtext.common.Terminals

import "platform:/resource/Questionaire/model/QuestionaireModel.ecore" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Questionaire returns Questionaire:
	name=EString 										
	'email:' resultEmail=EString						
		(elements+=Element ( elements+=Element)* )?		
	;

Element returns Element: Heading | Paragraph | TextQuestion | ChoiceQuestion | MatrixQuestion | CalendarQuestion | IntegerQuestion;
Heading returns Heading: '#'text=EString;
Paragraph returns Paragraph: '!'text=EString;

QuestionBase:
	'?' title=EString (mandatory?='*')?	
	(description=EString)?	
	(conditions+=QuestionCondition)*
;

TextQuestion:
	questionBase=QuestionBase
	'text[' (lines=EInt) ']'						
	;

terminal SHORT_LONG returns ecore::EInt: 'short' | 'long';

ChoiceQuestion:
	questionBase=QuestionBase
	'choice[' (minSelections=EInt | (minSelections=EInt 'to' maxSelections=EInt)) ']'
	options+=Option+
	;
Option: '-'text=EString('{' name=EString '}')?;

MatrixQuestion:
	questionBase=QuestionBase																	
	'matrix[' maxPerRow=EInt '][' (columnNames+=EString ( "," columnNames+=EString)* ']')?		
	('-' rowNames+=EString)+																	
	;

CalendarQuestion:
	questionBase=QuestionBase
	'calendar[' (	(year?='year')? ','? 
					(month?='month')? ','? 
					(day?='day')? ','? 
					(hour?='hour')? ','? 
					(minute?='minute')?
				) ']'
	;

IntegerQuestion:
	questionBase=QuestionBase
	'number[' minValue=EInt 'to' maxValue=EInt ( 'step' step=EInt)? ']'
	;

QuestionCondition: '{'option+=[Option|EString] ( "/" option+=[Option|EString])*'}';

EInt returns ecore::EInt: '-'? INT;
EString returns ecore::EString:	STRING | ID;
